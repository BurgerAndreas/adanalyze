
# ============Settings==========
set term png enhanced size 2000, 1800 font "Verdana, 20"
set key at 4.5,4.5
set style line 11 lc rgb '#808080' lt 1
set size square
set size ratio -1
set xrange[-4.8:4.8] # Garanties consistent size between plots. 
set yrange[-4.8:4.8] 
set xlabel "x / {/Symbol l}m" font "Verdana, 30"
set ylabel "y / {/Symbol l}m" font "Verdana, 30"
unset title

# ============File locations==========
# Files to plot.
f="zwischenf_2_600"

# File location.
s="Output/"

# Output location.
d="Output/Plots/"
 
  
# ============Triangulation==========  
set cbrange [4:8] # <4 Adjacencies same as =4.
set palette rgbformulae 33,13,10 
set cbtics 1 
set output d.f."_triangulation.png"
plot s.f."_triangulation.dat" using 1:2 with linespoints title "Triangulation" ls 1 lc rgb "grey", "" using 1:2:3 with points title "Anzahl Adjazenten" pt 7 ps 2 lc palette


# ============Settings for everything energy==========
set size square
set size ratio -1
set xrange[-4.8:4.8] # Garanties consistent size between plots.  
set yrange[-4.8:4.8] 
unset cbrange
set cbtics auto
#set palette rgbformulae 7,5,15 # Alternative colour palette (black-blue-red-yellow).
load 'moreland.pal' # Personal favourite.



# ============Energyshifted==========
set cbrange [-1.5:1.5]
set output d.f."_dEshift.png"
plot s.f."_dEshift.dat" using 1:2:3:4:5 with vectors title "{/Symbol D}E(shift) / Distanz" filled lw 3 lc palette, s.f."_pinned.dat" using 1:2 title "gepinnte Flusslinien" pt 7 ps 1 lc rgb "black"



# ============Energie-Difference adjacent points==========
unset cbrange
#set cbrange [-0.5:0.5]
set cbtics auto
set output d.f."_dEadjacent.png"
plot s.f."_dEad.dat" using 1:2:3:4:5 with vectors title "{/Symbol D}E(Adjazenten) / Abstand" filled lw 3 lc palette




