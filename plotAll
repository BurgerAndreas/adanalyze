
# ============Allgemeine Einstellungen==========
set term png enhanced size 2000, 1800 font "Verdana, 20"
set key outside
set style line 11 lc rgb '#808080' lt 1
set size square
set size ratio -1
set xrange[-4.8:4.8] # Das garantiert gleiche Größe zwischen verschiedenen plots. 
set yrange[-4.8:4.8] # Vereinfacht Vergleiche.
set xlabel "x / {/Symbol l}m" font "Verdana, 30"
set ylabel "y / {/Symbol l}m" font "Verdana, 30"

# ============Speicherorte==========
#Welche files sollen geplottet werden?
f="zwischenf_300"

#Wo sind die files gespeichert?
s="Output/"

#Wo sollen die plots hin?
d="Output/Plots/"


# ============Indices Original==========
#set title "Indices of original points" font "Verdana, 40"
#set output d.f."_indices.png"
#plot s.f."_indices.dat" using 1:2:3 with labels notitle


# ============Indices Alle mit Box==========
#xl=7.86 #muss von Hand eingetragen werden
#yl=6.98
#set arrow 1 from xl/2,-4 to xl/2,4 nohead  lw 2 lc rgb "dark-orange" # Box.
#set arrow 2 from -4.5,yl/2 to 4.5,yl/2 nohead lw 2 lc rgb "dark-orange" 
#set arrow 3 from -xl/2,-4 to -xl/2,4 nohead lw 2 lc rgb "dark-orange" 
#set arrow 4 from -4.5,-yl/2 to 4.5,-yl/2 nohead lw 2 lc rgb "dark-orange" 

#set title "Indices of all points with marked perimeter" font "Verdana, 40"
#set output d.f."_indicesfull.png"
#plot s.f."_indicesfull.dat" using 1:2:3 with labels notitle font "8, 14" tc rgb "grey"
#unset arrow


# ============Adjazenten==========
#set cbrange [4:8]
#set palette rgbformulae 33,13,10
#set cbtics 1
#set title "Number of adjacencies" font "Verdana, 40"
#set output d.f."_adjacencies.png"
#plot s.f."_adjacencies.dat" using 1:2:3 with points notitle pt 7 ps 2 lc palette
  
  
# ============Triangulation==========  
set cbrange [4:8] # <4 Adjazenten bekommen gleiche Farbe wie =4.
set palette rgbformulae 33,13,10 
set cbtics 1 # Farbskala Schritte.
set title "Full triangulation with number of adjacencies" font "Verdana, 40"
set output d.f."_triangulation.png"
plot s.f."_triangulation.dat" using 1:2 with linespoints notitle ls 1 lc rgb "grey", "" using 1:2:3 with points notitle pt 7 ps 2 lc palette


# ============Einstellungen für Energien-plots==========
# Parameter für folgende zwei Plots. Andere Farben und ev. Größe,
# da beide abhängig von der "region of interest" sind.
set size square
set size ratio -1
set xrange[-4.8:4.8] # Das garantiert gleiche Größe zwischen verschiedenen plots. 
set yrange[-4.8:4.8] # Vereinfacht vergleiche.
unset cbrange
set cbtics auto
#set palette rgbformulae 7,5,15 # Alternative Farb-palette (black-blue-red-yellow).
set loadpath "/home/andreasb/Projekte/Gnuplot/"
load 'moreland.pal' # Schöne Farben.
unset loadpath


# ============Energie shifted==========
set title "E(shift)-E(regular) divided by shift length" font "Verdana, 40"
set output d.f."_dEshift.png"
plot s.f."_dEshift.dat" using 1:2:3:4:5 with vectors notitle filled lw 3 lc palette


# ============Energie-Differenz Nachbarn==========
unset cbrange
set cbtics auto
set title "E(adjacent)-E(point) divided by distance" font "Verdana, 40"
set output d.f."_dEadjacent.png"
plot s.f."_dEadjacent.dat" using 1:2:3:4:5 with vectors notitle filled lw 3 lc palette

